@go.Package { name = "github.com/m-mizutani/hatchery/pkg/domain/config" }
module org.github.m_mizutani.hatchery.config

import "package://pkg.pkl-lang.org/pkl-go/pkl.golang@0.5.3#/go.pkl"

abstract class Action {
    id: String(this.matches(Regex(#"^[A-Za-z0-9][A-Za-z0-9-_]{1,128}[A-Za-z0-9]$"#)))

    // Destination
    bucket: String(this.matches(Regex(#"^[a-z0-9][a-z0-9-]{1,61}[a-z0-9]$"#)))
    prefix: String?
}

class OnePassword extends Action {
    api_token: String // No validation to avoid leaking to logs
    duration: Duration(this > 1.s) = 20.min
    limit: Int(this > 0) = 1000
    max_pages: Int(this > 0)?
}

class FalconDataReplicator extends Action {
    aws_region: String(this.matches(
        Regex(#"^(us|ap|ca|cn|eu|sa)-(central|east|northeast|southeast|west|south|north)-\d+$"#))
    )
    aws_access_key_id: String(this.matches(Regex(#"^[A-Z0-9]{20}$"#)))
    aws_secret_access_key: String // No validation to avoid leaking to logs
    sqs_url: String(this.matches(Regex(#"^https:\/\/sqs\.[a-zA-Z0-9\-]{3,}\.amazonaws\.com\/\d{12}\/[a-zA-Z0-9_-]+$"#)))
}

actions: List<Action>
